configfile: "config/config.yaml"

rule all:
    input:
        "resources/bac120_taxonomy_r{}.tsv".format(config["gtdb_version"]),
        "resources/selected_proteomes",
        "results/genome2abbrev.csv",
        expand("results/mcl_clusters_I{inf}.txt", inf=config["mcl"]["inflation"])

rule get_all_proteomes:
    output:
        "resources/all_proteomes.tar.gz",
    shell:
        "wget '{config[gtdb_base_url]}release{config[gtdb_version]}/{config[gtdb_version]}.{config[gtdb_subversion]}/genomic_files_reps/gtdb_proteins_aa_reps_r{config[gtdb_version]}.tar.gz' -O {output}"

rule get_all_genomes:
    output:
        "resources/all_genomes.tar.gz"
    shell:
        "wget '{config[gtdb_base_url]}release{config[gtdb_version]}/{config[gtdb_version]}.{config[gtdb_subversion]}/genomic_files_reps/gtdb_genomes_reps_r{params.gtdb_version}.tar.gz' -O {output}"

rule extract_all_proteomes:
    input:
        "resources/all_proteomes.tar.gz"
    output:
        directory(config["all_proteomes_outdir"])
    shell:
        """
        mkdir {config[all_proteomes_outdir]}
        tar -xzf {input} -C {config[all_proteomes_outdir]} --strip-components=1
        """

rule extract_all_genomes:
    input:
        "resources/all_genomes.tar.gz"
    output:
        directory(config["all_genomes_outdir"])
    shell:
        """
        mkdir {config[all_genomes_outdir]}
        tar -xzf {input} -C {config[all_genomes_outdir]} --strip-components=1
        """

rule select_proteomes:
    input:
        config["accession_ids_file"],
        config["all_proteomes_outdir"]
    output:
        directory("resources/selected_proteomes")
    shell:
        "workflow/scripts/select_proteomes.sh {input[0]} {input[1]} {output}"

rule select_genomes:
    input:
        config["accession_ids_file"],
        config["all_genomes_outdir"]
    output:
        directory("resources/selected_genomes")
    shell:
        "workflow/scripts/select_genomes.sh {input[0]} {input[1]} {output}"

rule get_bacterial_taxonomy_file:
    output:
        "resources/bac120_taxonomy_r{}.tsv.gz".format(config["gtdb_version"])
    shell:
        "wget '{config[gtdb_base_url]}release{config[gtdb_version]}/{config[gtdb_version]}.{config[gtdb_subversion]}/bac120_taxonomy_r{config[gtdb_version]}.tsv.gz' -O {output} || wget '{config[gtdb_base_url]}release{config[gtdb_version]}/{config[gtdb_version]}.{config[gtdb_subversion]}/bac_taxonomy_r{config[gtdb_version]}.tsv.gz' -O {output}"
	
rule unzip_bacterial_taxonomy_file:
    input:
        "resources/bac120_taxonomy_r{}.tsv.gz".format(config["gtdb_version"])
    output:
        "resources/bac120_taxonomy_r{}.tsv".format(config["gtdb_version"])
    shell:
        "gzip -d {input}"

rule create_genome2taxa:
    input:
        config["accession_ids_file"],
        "resources/bac120_taxonomy_r{}.tsv".format(config["gtdb_version"])
    output:
        "results/genome2taxa.csv"
    shell:
        """
        REGEXP=`tr '\\n' '|' < {input[0]} | sed 's/|$//'`
        grep -E "$REGEXP" {input[1]} | sed -E 's/\\t+/,/g' > {output}
        if [ -s "{config[manually_add_taxa_file]}" ]; then
             echo "Appending contents of file {config[manually_add_taxa_file]} to {output}"
             cat {config[manually_add_taxa_file]} >> {output}
        else
             echo "{config[manually_add_taxa_file]} not existing or empty"
        fi
        """

rule generate_shortcodes:
    input:
        "results/genome2taxa.csv"
    output:
        "results/genome2abbrev.csv"
    shell:
        "workflow/scripts/rename_genomes.py {input} {output}"

rule cat_all_sequences:
    input:
        "resources/selected_proteomes"
    output:
        "results/all_proteomes.faa"
    shell:
        "cat {input}/*.faa > {output}"

rule make_diamond_db:
    input:
        "results/all_proteomes.faa"
    output:
        "results/proteomes.dmnd"
    threads: config["diamond"]["threads"]
    shell:
        "diamond makedb --in {input} -d {output} --threads {threads}"

rule diamond_all_vs_all:
    input:
        db="results/proteomes.dmnd",
        query="results/all_proteomes.faa"
    output:
        "results/all_vs_all_wo_selfhits.tsv"
    resources:
        mem_mb_per_cpu=5000,
        # 1 day runtime
        runtime=1440
    threads: config["diamond"]["threads"]
    shell:
        """
        diamond blastp \
                --db     {input.db} \
                --query  {input.query} \
                --out    {output} \
                --outfmt 6 qseqid sseqid pident bitscore evalue qcovhsp scovhsp \
                --more-sensitive \
                --threads {threads}
        """

rule add_self_hits_to_diamond_all_vs_all:
    input:
        query="results/all_proteomes.faa",
        diamond="results/all_vs_all_wo_selfhits.tsv"
    output:
        "results/all_vs_all.tsv"
    shell:
        """
        cp {input.diamond} {output}
        # ---------- guarantee self-hits so singletons survive ---------- 
        # q s pident bits evalue qcov scov  (7 cols, tab-separated)
        awk '/^>/{{sub(/^>/,"",$0); printf "%s\\t%s\\t100\\t1000\\t1e-200\\t100\\t100\\n",$0,$0 }}' "{input.query}" >> "{output}"
        """


rule filter_and_convert_into_mcl_s_abc_format:
    input:
        "results/all_vs_all.tsv"
    output:
        "results/all_vs_all.abc"
    params:
        evalue_cut=config["diamond"]["evalue_cut"],
        cov_cut=config["diamond"]["cov_cut"]
    shell:
        "workflow/scripts/filter_and_convert.py -i {input} -o {output} -e {params.evalue_cut} -c {params.cov_cut}"

rule mcl_sweep:
    input:
        "results/all_vs_all.abc"
    output:
        "results/mcl_clusters_I{inflation}.txt"
    resources:
        mem_mb_per_cpu=20000,
        runtime=1440
    shell:
        "mcl {input} --abc -I {wildcards.inflation} -o {output}"

rule split_mcl_clusters:
    input:
        fasta="results/all_proteomes.faa",
        cluster="results/mcl_clusters_I{inflation}.txt"
    output:
        directory("results/mcl/{inflation}/Normal/")
    params:
        huge_cutoff=300
    shell:
        "workflow/scripts/split_clusters.py -f {input.fasta} -c {params.huge_cutoff} -m {input.cluster} -o {output}"

rule download_eggnog_diamond_database:
    output:
        directory("resources/eggnog_diamond_db")
    shell:
        "download_eggnog_data.py -f --data_dir {output}"

rule eggnog_mapper:
    input:
        fasta="results/all_proteomes.faa",
        db="resources/eggnog_diamond_db"
    output:
        directory("results/eggnog/annotations")
    threads: config["eggnog"]["threads"]
    resources:
        mem_mb_per_cpu=4000,
        runtime=2880
    params:
        mode="diamond"
    shell:
        """
        emapper.py -i {input.fasta} --cpu {threads} --data_dir {input.db} -m {params.mode} --output {output} --override
        """

rule mafft_align:
    input:
        "results/mcl/{inflation}/Normal/{msa}.faa"
    output:
        "results/mafft/{inflation}/{msa}.aln"
    threads: config["mafft"]["threads"]
    resources:
        mem_mb_per_cpu=4000,
        runtime=600
    shell:
        "mafft --auto --thread {threads} {input} > {output}"

rule trimal_trim:
    input:
        "results/mafft/{inflation}/{msa}.aln"
    output:
        "results/trimal/{inflation}/{msa}.trim"
    resources:
        mem_mb_per_cpu=3000,
        runtime=600
    shell:
        "trimal -in {input} -out {output} -gt 0.1"

rule alignment_statistics:
    input:
        "results/trimal/{inflation}/"
    output:
        "results/alignment_statistics_I{inflation}.txt"
    shell:
        "workflow/scripts/alignment_stats.py {input} {output}"
